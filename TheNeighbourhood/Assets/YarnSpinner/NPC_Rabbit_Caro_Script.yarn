#__PrivCrochet_version:1
__PrivCrochet_colorID: 0
__PrivCrochet_position: 787,326
__PrivCrochet_tags: 
title: Start_Intro
position: -242,-431
---
///Name of gained item
<<declare $gainedItem_Name to "">>
///Do Player has FrozenPizza?
<<declare $hasFrozenPizza to false>>
//
///Do Player has CookedPizza?
<<declare $hasCookedPizza to false>>
///Has Player use CookedPizza
<<declare $useCookedPizza to false>>
//
///Do Player need to get Hamantash?
<<declare $needHamantash to false>>
///Do Player has Hamantash?
<<declare $hasHamantash to false>>
///Has Player use Hamantash?
<<declare $useHamantash to false>>
//
///Has Player unbaked Challah?
<<declare $hasUnbakedChallah to false>>
///Has Player use unbaked Challah?
<<declare $useUnbakedChallah to false>>
//
///Has Player baked Challah?
<<declare $hasBakedChallah to false>>
///Has Player use baked Challah?
<<declare $useBakedChallah to false>>
//
///Is Player on the quest to find 3 Ingredients?
<<declare $findIngredients to false>>
//
//Disable DoorCollider at start
<<set $unlockedDoor_ToHallway to false>>
<<Un_LockDoor Door_ToHallway ToHallway false false>>
// Dieser Dialog wird ganz zu Beginn gespielt. Die Szene soll sein, wie die beiden die letzten Kisten nach oben gebracht haben
Lilly: ... Tell me those were the last boxes.
Caroline: Yes, thankfully! It only took us three months to get the rest of our most important stuff in here...
Caroline: So, welcome home to us, at last!
Lilly: Welcome home, indeed!
Caroline: Phew. Well, I don't know about you, but I am starving. And falling asleep. Both, simultaneously.
Lilly: I got you! Pretty sure I bought some frozen pizza yesterday, I can bring you yours when it's done, ok?
Caroline: You're a blessing! Definitely one of my top 3 roommates. What? The other two are my bed and my PC, no front. 
Lilly: I'm competing with hardware and furniture? Ouch.
// An diesem Punkt soll der Dialog beendet werden und Caroline in ihr Zimmer verschwinden
<<TargetMove NPC_Rabbit 0 NPC_Rabbit_MoveGoal false>>
<<jump Movement_Tutorial>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1024,326
__PrivCrochet_tags: 
title: Movement_Tutorial
position: -243,-366
---
Lilly: Alright then, frozen pizza time. // Dieser String soll auftauchen, nachdem Caroline in ihr Zimmer gegangen ist
<i>(After this dialogue use your mouse to click where you want to go)</i> // Dies soll als Message nach der ersten Line separat auftauchen
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 0,0
__PrivCrochet_tags: position: -194,-200
position: -121,-274
title: TalkTo_NPC_Rabbit_Caroline
---
//This node is like a cross-road for all possible scenarios
//We go from the latest in the story to the youngest.
<<if $hasBakedChallah>>
    //If player at the end and has bakedChallah
    Caroline: What are you waiting for? Go to your grandparents who are waiting for you!
    <<set $unlockedDoor_ToHallway to true>>
    <<Un_LockDoor Door_ToHallway ToHallway true true>>
<<elseif $hasUnbakedChallah>>
    Caroline: All we need is to bake these!
<<elseif $hasTalked_NPC_Wolf && $hasTalked_NPC_Squirrel && $hasTalked_NPC_Bird && $getAllIngredients>>
    //If Lilly has talked to Wolf, Bird, Squirrel and got all 3 ingredients
    <<jump Caro_After_Ingredients>>
<<elseif $hasTalked_Grandma == true && $hasTalked_Grandpa == true>>
    //If Lilly talked to Grandparents.
    <<jump Caro_After_Grandparents>>
<<elseif $hasHamantash == true && $hasTalked_Grandma == false && $hasTalked_Grandpa == false>>
    //If Lilly hasn't talk to everyone but already got the Hamantash in the Fridge.
    Caroline: Back so soon?
    <<set $unlockedDoor_ToHallway to true>>
    <<Un_LockDoor Door_ToHallway ToHallway true true>>
<<else>>
    <<jump Bringing_Pizza_to_Roommate>>
<<endif>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1260,329
__PrivCrochet_tags: 
title: Fridge_Interaction
position: -304,-276
---
//If Lilly doesn't have Frozen Pizza yet
<<if $needHamantash == false && $hasFrozenPizza == false && $hasCookedPizza == false>>
    // Dieser Monolog passiert, wenn man den Kühlschrank auf der Suche nach den TK-Pizzen untersucht
    Lilly: Oh wow, besides my pastry for tonight there's barely any food left. We really should've done a thorough grocery run before unpacking. 
    Lilly: Huh? No pizza? I must've forgotten to put it in the freezer… welp, wherever it is, let's hope it's still edible.
<<elseif $needHamantash == true && $useHamantash == false && $hasFrozenPizza == false>>
    //If Player needs Hamantash but hasn't use is AND has no FrozenPizza (as they use it already)
    <<jump Hamantash_Fridge_Interaction>>>
<<else>>
    //Generic Fridge interaction
    Lilly: Who can live without a fridge?
<<endif>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1493,327
__PrivCrochet_tags: 
title: Picking_Up_Pizza_Monologue
position: 87,-456
---
// Dieser Dialog kommt auf, wenn man die Pizza z. B. in dem eigenen Zimmer findet und mit E aufhebt
//Piza located in Bag
<<if $hasFrozenPizza == false && $hasCookedPizza == false && $useCookedPizza == false>>
    Lilly: There you are! Still looking good. To the oven with you!
    <<set $hasFrozenPizza to true>>
    <<set $gainedItem_Name to "FrozenPizza">>
    <<AccessYarnGainedItemValue Yarn_InventoryManager>>
    <<DisplayGainedItem Yarn_InventoryManager>>
<<else>>
    Lilly: My purple backpack!
<<endif>>
//Lilly: There you are! Still looking good. To the oven with you!
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1734,329
__PrivCrochet_tags: 
title: Interact_Oven
position: 89,-380
---
<<if $getAllIngredients && $hasUnbakedChallah && !$hasBakedChallah>>
    //If player has all 3 ingredients -> Interact with table and get unbaked Challah
    //But havent have baked Challah yet
    <<jump Baking_Challah_Oven>>
<<elseif !$getAllIngredients && $hasFrozenPizza == true>>
    //If player isn't on the ingredients quest but has FrozenPizza
    <<UseGainedItem Yarn_InventoryManager>>
    Lilly: The smell... Yum!
    <<set $gainedItem_Name to "Pizza">>
    <<set $hasCookedPizza to true>>
    <<set $hasFrozenPizza to false>>
    <<AccessYarnGainedItemValue Yarn_InventoryManager>>
    <<DisplayGainedItem Yarn_InventoryManager>>
    //Caro move to table
    //<<TargetMove NPC_Rabbit Table false>>
    Lilly: Time to get back to Caroline.
<<else>>
    Lilly: Makes me think of the time I tried to bake pizza in there. Bad idea, Lilly, bad idea. 
<<endif>>
// Ob der Dialog sinnvoll ist, wird sich zeigen, aber er soll eintreten, wenn man die TK-Pizzen zum Ofen bringt und mit E backt
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1969,328
__PrivCrochet_tags: 
title: Bringing_Pizza_to_Roommate
position: 90,-303
---
Caroline: Do you have the pizza yet?
-> Lilly: Ah, not yet. <<if $hasCookedPizza == false>>
    //Ich habe das hier zum test geadded.
    Caroline: No rush. Great things take time. 
-> Lilly: We just ate it... <<if $useCookedPizza == true>>
    Caroline: Oh true...
-> Lilly: Yup! <<if $hasCookedPizza == true>>
    Caroline: Smells like victory! 
    // Dieser Dialog findet statt, wenn man die gebackenen Pizzen zu Caroline bringt 
    Lilly: We do deserve a medal after all those weeks of unpacking. So, where do we go from here?
    Caroline: I'd suggest a nap and afterward ignoring the boxes we just brought up and instead of finishing our series marathon. 
    Caroline: … you don't look convinced. Fine, we can also work on our dreadfully boring game art assignment. Just be warned - I'm taking coding classes for a reason. 
    Lilly: It's not that. I'll just be over at my grandparents' in the evening and I'm thinking, maybe I should go there sooner. 
    Caroline: Oh. Did I miss an upcoming birthday party? Please tell me I didn't, I saw your grandma in the hallway yesterday and would've said something if I knew. 
    Lilly: Don't worry, they barely know their own birthdays. But no, today is the International Holocaust Remembrance Day. I know, it's a mouthful. 
    Lilly: But it means a lot to my grandparents. And they mean a lot to me, so.
    Caroline: I see. That's what you were baking for, right?
    Lilly: Yeah, that's our tradition! Well, it has become our tradition. 
    Lilly: A few years back it used to be a day on which my grandparents were mostly alone, sorrowing and sad, which was difficult to witness. 
    Lilly: I eventually decided to join them and over the years it just turned into a day of coming together and celebrating our Jewish culture
    Lilly: while remembering those that were left behind. 
    Caroline: I love that! And you do seem to have a great bond with your grandparents. Well then, don't let me stop you from checking on them!
    <<Toggle_CharacterActive NPC_Rabbit true>>
    Lilly: You're right, I think it's a good idea to see how they're doing. 
    // Nach dieser Line sollte Caro wieder in ihrem Zimmer verschwinden.
    Lilly: I should take the Hamantashen pastry I made with me, so they know I did my part. 
    Lilly: At least I managed to put this one <b>in the fridge.</b>
    <<set $needHamantash to true>>
    //Use Pizza
    <<set $useCookedPizza to true>>
    <<UseGainedItem Yarn_InventoryManager>>
    //Enable DoorCollider back
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 2210,330
__PrivCrochet_tags: 
title: Hamantash_Fridge_Interaction
position: -304,-120
---
// Spieler gehen zum Kühlschrank und holen Hamantaschen raus
Lilly: What a fantastic smell! Let's see how my grandparents are doing downstairs. 
//Hamantash Gebäck erscheint im Inventar
<<set $hasHamantash to true>>
<<set $needHamantash to false>>
<<set $gainedItem_Name to "Hamantash">>
<<AccessYarnGainedItemValue Yarn_InventoryManager>>
<<DisplayGainedItem Yarn_InventoryManager>>
// Tür zum Treppenhaus wird unlocked
//These two needs to go together
<<set $unlockedDoor_ToHallway to true>>
<<Un_LockDoor Door_ToHallway ToHallway true true>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 786,584
__PrivCrochet_tags: 
title: Caro_After_Grandparents
position: -17,-94
---
// Voraussetzung: Man hat zum ersten Mal mit den Grandparents geredet und erfahren, dass man das Challah-Brot backen muss. 
// Nun geht man zurück in die WG
// Man interagiert mit Caros Tür, sie selbst ist noch nicht zu sehen
<<set $unlockedDoor_ToHallway to false>>
<<Un_LockDoor Door_ToHallway ToHallway false false>>
Caroline: ...
-> Lilly: I should hurry... <<if $findIngredients == true>>
-> Lilly: Caro!
    Caroline: Zzz… AH! 
    Caroline: Who is it? // Caro erscheint danach vor der Tür
    <<TargetSpawn NPC_Rabbit 2 NPC_Rabbit_MoveGoal false>>
    <<ShowSprite NPC_Rabbit true>>
    <<FlipSprite NPC_Rabbit>>
    Caroline: Lil-
    Lilly: I forgot to bake the Challah!
    Caroline: The what?
    Lilly: My grandma's favorite pastry! For our dinner!
    Lilly: This is a crisis! Our fridge is almost empty, it's Sunday, I only have a few more hours and-
    Caroline: Hey, Lilly, it's alright, it's fine! 
    Caroline: Why don't you just tell your grandparents exactly what you just told m-
    Lilly: I can't, I already lied!
    Lilly: Yeah, I panicked! 
    Lilly: I mean, what should I have said? 
    Lilly: “Hey Grandma, I know you're already not feeling well for one reason, so here's another”? 
    Lilly: Now they think I baked it already!
    Caroline: Oh, no, Lilly. You put yourself in serious trouble there.
    Lilly: Not helping!
    Caroline: Alright, alright. Hmm...
    Caroline: Hey, how about you ask our neighbours for the ingredients?
    Caroline: I mean, you're charming and friendly and it might even be a good chance to finally introduce us properly to our neighbours.
    Caroline: It's about time we switch out the awkward waving in the hallway with slightly less awkward small talk. 
    Lilly: ...
    Lilly: That's actually a good id- wait!
    Caroline: What is it?
    Lilly: If I tell them what I need the ingredients for, the first thing they'll know about me is that I'm Jewish. 
    Caroline: Uhh... I'm confused. 
    Caroline: Isn't it your plan to embrace your Jewish roots more and be more open about them?
    Lilly: Yes, but not as a first introduction - especially not on the International Holocaust Remembrance Day! 
    Lilly: I mean, “I'm Jewish” and “Holocaust” in the same sentence, Caro! What I'll say after that won't even matter anymore.
    Lilly: After such an introduction, the only thing standing between me from becoming a walking stereotype will be ...
    Lilly: ... the screen turning black and white ... // Screen turns black and white
    <<SetGlobalVolumeActive YarnEffectsManager true>>
    Lilly: ... and a distant sentimental violin playing ... // distant sad violin playing
    Lilly: ... before clouds form inside our building and rain starts to pour! // rain starts to pour
    <<SetRainEffectActive YarnEffectsManager true>>
    Lilly: And from that moment on, Caro, my fate will be sealed - no other mentions about who I am will make me escape that black and white filter. 
    Lilly: I will officially be one-dimensional. 
    <<SetRainEffectActive YarnEffectsManager false>>
    <<SetGlobalVolumeActive YarnEffectsManager false>>
    Caroline: ...
    Caroline: Whew... 
    Caroline: That sounds like a lot to carry around all the time, so I'm glad you let it out!
    Caroline: But Lilly, we will be staying here at least for a few more months. 
    Caroline: That's enough time for any neighbour to get to know you as a talented artist, a passionate film critique-
    Caroline: Or even just as the lovely chaotic roommate that you are.
    Caroline: And maybe our neighbours already know about your Jewish roots from your grandparents. 
    Lilly: I doubt it, they rarely talk about it outside our family. 
    Lilly: But maybe you're right ... maybe it will go better than expected. 
    Lilly: And yet, I'm already stressing about what the conversations will be like. 
    Lilly: Are you sure you don't wanna join?
    Caroline: I would, but I really need to get some more sleep before pulling an all-nighter for that darn game art assignment. 
    Caroline: But I would suggest you start with the neighbour above your grandparents' apartment. He seems like a cool dude. 
    Lilly: Alright, thanks. Wish me luck! // Wolf-Tür-Interaktion wird unlocked, Caro verschwindet im Zimmer (geht schlafen)
    Lilly: (From what I could tell I am missing 3 ingredients: Flour, oil and eggs.)
    <<set $findIngredients to true>>
//
<<set $unlockedDoor_NPC_Wolf to true>>
//We will unlock door at Start() in Hallway Scene
<<set $unlockedDoor_ToHallway to true>>
<<Un_LockDoor Door_ToHallway ToHallway true true>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 786,842
__PrivCrochet_tags: 
title: Caro_After_Ingredients
position: -116,-9
---
//Lock the ToHallway_Door
<<set $unlockedDoor_ToHallway to false>>
<<Un_LockDoor Door_ToHallway ToHallway false false>>
// Voraussetzung: Man hat alle Zutaten von den Nachbarn geholt und ist danach zurück in die Wohnung gegangen, um das Challah-Brot zu backen
// Caro steht direkt neben der Wohnungstür als man reinkommt, es wird ein automatischer Dialog getriggert
Caroline: How did it go?
//Debug and make sure important vars are true
<<set $getAllIngredients to true>>
Lilly: Caro? Didn't you want to sleep?
Caroline: I did. But then I wanted to know how it went because you were so worried, and I wasn't sure if I should've gone with you or-
Caroline: Oh, looks like you got everything!
Lilly: Yeah! Don't worry, it was awkward at first because I lied - and you know how bad I am at lying.
Lilly: But then I came clean, which made it way easier and: turns out we have some pretty great neighbours!
Caroline: What a relief! I'm glad it went well, Lilly!
Caroline: You still have to bake that pastry, don't you?
Lilly: I do, and I hope I'll be able to finish it on time. 
Caroline: Need a hand?
Lilly: What about your plans?
Caroline: You know, baking a pastry to fix a well-intentioned lie seems like the perfect way to procrastinate. 
Lilly: I won't say no to that, thank you! Well then, let's assemble the Challah at our table. 
// Dialog endet, Caro geht zum Tisch
<<TargetMove NPC_Rabbit 3 Table false>>
// Caro steht neben dem Herd
// Man hat eine Konversation mit Caro, an einem Punkt in der Konversation beschließt man, mit dem Backen loszulegen
// -> Zutaten verschwinden aus dem Inventar, wenig später erscheint das fertige Challah-Brot im Inventar
// Caro sagt noch 1, 2 Dinge und dann geht man zu den Grandparents
// -> Grandparents letzte Dialoge werden freigeschaltet
===
__PrivCrochet_colorID: 8
__PrivCrochet_position: 1012,843
__PrivCrochet_tags: 
title: Making_Challah_With_Lilly
position: -115,156
---
// Monolog wird über Interaktion mit dem Tisch getriggert
// Zutaten aus dem Inventar verschwinden
<<UseAllIngredients Yarn_InventoryManager>>
// Ungebackenes Challah erscheint im Inventar
<<set $hasUnbakedChallah to true>>
<<set $gainedItem_Name to "UnbakedChallah">>
<<AccessYarnGainedItemValue Yarn_InventoryManager>>
<<DisplayGainedItem Yarn_InventoryManager>>
Lilly: We nailed it. Into the oven with our creation!
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1255,842
__PrivCrochet_tags: 
title: Baking_Challah_Oven
position: -115,75
---
// Dialog wird über Interaktion mit dem Ofen getriggert
//Use UnbakedChallah from Inventory
<<UseGainedItem Yarn_InventoryManager>>
Caroline: Are you drooling? I'm drooling. No wonder it's your grandma's favorite!
Lilly: We finished right on time! I'll make sure to put some aside for you.
<<set $hasBakedChallah to true>>
<<set $hasUnbakedChallah to false>>
<<set $gainedItem_Name to "BakedChallah">>
<<AccessYarnGainedItemValue Yarn_InventoryManager>>
<<DisplayGainedItem Yarn_InventoryManager>>
Caroline: Now that's what I want to hear! 
Caroline: Hey, Lilly. I wish you a great evening with your grandparents, enjoy the food!
Lilly: Thank you, Caro.
<<set $unlockedDoor_ToHallway to true>>
<<Un_LockDoor Door_ToHallway ToHallway true true>>
===
